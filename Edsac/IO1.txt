























[18 Обмен в массиве элементов попарно. Если 1-е число больше 2-го меняем их местами.
Если 3-е число больше 4-го меняем их местами. И т.д.
ПРИМЕР ВХОДНЫХ ДАННЫХ: массив(10,9,8,7,6,5,4,3,2,1,0)
ОТВЕТ В ЯЧЕЙКАХ: 88 - 98]


 T 99 S
 X 0 S
 T 0 S [ обнуляем аккумулятор ]
 A 86 [len] S [ загружаем в аккум. длину массива ]
 T 1 S [ записываем длину массива в ячейку 1 ]
 A 87 [addr] S [ загружаем в аккум. адрес 0-го элемента массива ]
 L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
 A 61 [r1] S [ прибавляем код инструкции с нулевым полем адреса ]
 U 61 [r1] S [ запиcываем сформированную инструкцию r1 ]
 U 57 [c1] S [ запиcываем сформированную инструкцию c1 ]
 A 85 [c] S [ прибавление 1 к полю адреса ]
 T 63 [r2] S [ запиcываем сформированную инструкцию r2 ]
 A 87 [addr] S [ загружаем в аккумулятор адрес 0-го элемента массива ]
 L 0 L [ сдвиг аккумулятора на 1 разряд влево ]
 A 64 [w1] S [ прибавляем код инструкции w1 с нулевым полем адреса ]
 U 64 [w1] S [ запиcываем сформированную инструкцию w1 ]
 A 85 [c] S [ прибавление 1 к полю адреса ]
 T 66 [w2] S [ запиcываем сформированную инструкцию w2 ]
 A 87 [addr] S [ загружаем в аккум. адрес массива ]
 L 0 L [ сдвиг аккум. на 1 разряд влево ]
 A 58 [c2] S [ прибавляем код инструкции c2 с нулевым полем адреса ]
 T 58 [c2] S [ записываем сформированную инструкцию c2 ]
[loop:] A 1 S [ загружаем счетчик необработанных элементов массива ]
 S 85 [c] S [ уменьшаем на 2 ]
 G 84 [exit] S [ если результат меньше 0, завершаем работу ]
 T 1 S [ обновляем значение счетчика и обнуляем аккумулятор ]
[с1:] A 0 S [ загружаем очередной элемент массива ]
[с2:] S 1 S [ отнимаем от него следующий ]
 G 67 S [ если разность отрицательна, то пропускаем блок перестановки ]
 T 2 S [ очищаем аккум. ]
[r1:] A 0 S [ загружаем в аккум. элемент с индексом N ]
 T 0 S [ записываем этот элемент в буферную ячейку ]
[r2:] A 1 S [ загружаем в аккум. элемент с индексом N+1 ]
[w1:] T 0 S [ записываем этот элемент в ячейку с адресом N ]
 A 0 S [ загружаем в аккум. значение из буферную ячейки 0]
[w2:] T 1 S [ записываем это значение в ячейку с адресом N+1 ]
 T 2 S [ очищаем аккум. ]
 A 63 [r2] S [ загружаем в аккум. код инструкции r2 ]
 A 85 [c] S [ прибавление 1 к полю адреса ]
 U 61 [r1] S [ запиcываем сформированную инструкцию r1 ]
 U 57 [m1] S [ запиcываем сформированную инструкцию m1 ]
 A 85 [c] S [ прибавление 1 к полю адреса ]
 T 63 [r2] S [ запись сформированной инструкции r2 ]
 A 66 [w2] S [ загружаем в аккум. код инструкции w2 ]
 A 85 [c] S [ прибавление 1 к полю адреса ]
 U 64 [w1] S [ запиcываем сформированную инструкцию w1 ]
 A 85 [c] S [ прибавление 1 к полю адреса ]
 T 66 [w2] S [ запиcываем сформированную инструкцию w2 ]
 A 85 [c] S [ загружаем в аккум. константу 2 ]
 L 0 L [ сдвиг на 1 разряд влево ]
 A 58 [m2] S [ прибавляем код инструкции m2 , исполненной на предыдущем шаге ]
 T 58 [m2] S [ записываем сформированную инструкцию ]
 E 53 [loop] S [ повторяем все операции; аккум. обнулен ]
[exit:] Z 0 S [ останов ]
[с:] P 1 S [ константа 2 ]
[len:]  P 5 L [ 11 - длина массива ]
[addr:] P 44 S [ 88 - адрес массива ]
[array:]  P 5 S [ 10 ]
 P 4 L [ 9 ]
 P 4 S [ 8 ]
 P 3 L [ 7 ]
 P 3 S [ 6 ]
 P 2 L [ 5 ]
 P 2 S [ 4 ]
 P 1 L [ 3 ]
 P 1 S [ 2 ]
 P 0 L [ 1 ]
 P 0 S [ 0 ]
